\chapter{Automatic Detection of Patterns using \ql{}}
\label{ap:ql}

\ql{}~\citep{avgustinovQLObjectorientedQueries2016} is ``a declarative, object-oriented logic programming language for querying complex,
potentially recursive data structures encoded in a relational data
model''.
\ql{} allows us to analyse programs at the source code level.
\ql{} extracts the source code of a project into a Data\-log model.
Besides providing structural data for programs, \ie{}, ASTs,
\ql{} has the ability to query static types and perform data-flow analysis.

In addition to gather cast usage data using \ql{}, given its powerfulness,
we have used \ql{} to approximate the automatic detection of some cast patterns.
This appendix gives an introduction to \ql{}.
Section~\ref{ap:ql:classes} provides the definition of several additional classes and predicates used in Chapter~\ref{cha:casts}.

\section{Introduction to \ql{}}

\ql{} is logic query language,
with a syntax that resembles both \sql{} and \java{}.
It is built up of logical formulas.
\ql{} uses logical connectives, \eg{}, \code{or} and \code{not},
quantifiers \code{exists} and \code{forall}, and logical predicates.
\ql{} is highly optimized to support recursive queries.
It is possible to use aggregates, \eg{}, \code{count} or \code{sum}, in \ql{} as well.

The source code ASTs are modeled as \ql{} classes,
\eg{}, the \code{CastExpr} class represents the table of cast expressions.
Unlike \java{},
\ql{} ``classes are just logical properties describing sets of already existing values.''%
\urlnote{https://help.semmle.com/QL/learn-ql/ql/about-ql.html}
For instance, Query~\ref{lst:ql:all} gets all cast expressions in a given project.
The \code{from} and \code{select} clauses have similar semantics as in \sql{}.
The \code{import} clause is used to select the language to be analysed.
\ql{} supports analysis of several languages,
\eg{}, \javascript{}, \python{}, \cc{}/\cpp{}, and \csharp{}.

\begin{listing}
\begin{minted}{\qllexer}
import java #\qlbox#

from CastExpr ce
select ce
\end{minted}
\caption{Query to fetch all cast expressions in a project.}
\label{lst:ql:all}
\end{listing}

The \code{where} clause is used to constraint the results.
The Query~\ref{lst:ql:unused-parameter} returns all unused parameters.
It \code{select}s both the unused parameter and the method where it is declared.
In this case, \code{where} is used similarly to a \sql{} \code{join} clause.
The \code{getAnAccess} class predicate returns any access---read or write---to that parameter.

\begin{listing}
\begin{minted}{\qllexer}
import java #\qlbox#

from Parameter p, Method m
where not exists(p.getAnAccess())
  and m.getAParameter() = p
  and not m.isAbstract()
select p, m
\end{minted}
\caption{Query to fetch unused parameters.}
\label{lst:ql:unused-parameter}
\end{listing}


The following query demonstrates how to use the \code{count} aggregate.
In this example it returns the number of methods with body,
\ie{}, neither \code{abstract} nor \code{native}.

\begin{listing}
\begin{minted}{\qllexer}
import java #\qlbox#

select count(Method m | exists(Block b | m.getBody() = b))
\end{minted}
\caption{Query to count methods with implementation.}
\label{lst:ql:count}
\end{listing}

\ql{} permits to define custom classes to refine query results. 
For example, the following query fetches all primitive cast expressions.
A primitive cast is a cast where both the target type and the type of the operand are primitive types.
This excludes any boxed type.
The class predicate defined in line 2 is called the \emph{characteristic predicate} or \emph{character} of a class.
It is the predicate that determines which values correspond to a given class.
Thus, it is similar to filter out results using the \code{where} clause.

\begin{listing}
\begin{minted}{\qllexer}
class PrimitiveCast extends CastExpr { #\qlbox#
	PrimitiveCast() {
		getExpr().getType() instanceof PrimitiveType and
		getTypeExpr().getType() instanceof PrimitiveType
	}
}

from PrimitiveCast ce
select ce
\end{minted}
\end{listing}

The following section describes the additional classes and predicates used throughout Chapter~\ref{cha:casts}.

\section{Additional \ql{} Classes and Predicates}\label{ap:ql:classes}

All cast pattern classes inherit from this base class.
As we have seen before,
the \code{CastExpr} is the \ql{} class that represents all casts.
Note that this class does not provide a characteristic predicate.
It just adds helper predicates to be used by detection patterns.

\begin{listing}
\begin{minted}{\qllexer}
class Cast extends CastExpr { #\qlbox#
	Type getTargetType() { result = getTypeExpr().getType() }
	Expr getExprOrDef() {
		result = getExpr() or
		exists (VariableAssign def |
			defUsePair(def, getExprOrDef()) and
			result = def.getSource()
		)
	}
}
\end{minted}
\caption{\code{Cast} class definition.}
\label{lst:ql:Cast}
\end{listing}

This class represents all upcasts.
An upcast \code{(T) e} happens when the type of \code{e} is a subtype of \code{T}.
Since to detect an upcast is needed to look-up in the class hierarchy,
the \code{+} operator---transitive closure operator---is used.

\begin{listing}
\begin{minted}{\qllexer}
class Upcast extends #\qlref{Cast}# { #\qlbox#
	Upcast() {
		getExpr().getType().(RefType).getASupertype+() = getTargetType()
	}
}
\end{minted}
\caption{\code{Upcast} class definition}
\label{lst:ql:Upcast}
\end{listing}

The following class represents when an argument is used in an overloaded method.

\begin{listing}
\begin{minted}{\qllexer}
class ArgumentEx extends Argument { #\qlbox#
	Parameter param;
	ArgumentEx() {
		call.getCallee().getParameter(pos) = param
	}
}

class OverloadedArgument extends ArgumentEx {
	Callable target;
	Callable overload;
	OverloadedArgument() {
		target = call.getCallee() and
		overload = target.getDeclaringType().getACallable() and
		overload.getName() = target.getName() and
		target != overload
	}
	Callable getTarget() { result = target }
	Callable getAnOverload() { result = overload }
}
\end{minted}
\caption{\code{OverloadedArgument} class definition.}
\label{lst:ql:OverloadedArgument}
\end{listing}

This class represents a cast applied to a variable.
A \ql{} variable is a field, a local variable or a parameter.
The \code{getADef} class predicate returns any definition for the variable being cast.
This is used in the \nameref{pat:VariableSupertype} pattern.

\begin{listing}
\begin{minted}{\qllexer}
 class VarCast extends #\qlref{Cast}# { #\qlbox#
	Variable var;
	VarCast() { var.getAnAccess() = getExpr() }
	Variable getVar() { result = var }
	Expr getADef() {
		exists (VariableAssign def |
			defUsePair(def, getExpr()) and
			result = def.getSource()
		)
	}
}
\end{minted}
\caption{\code{VarCast} class definition.}
\label{lst:ql:VarCast}
\end{listing}

The following predicate holds with expressions \code{e}, \code{f}, and \code{c} such that either
\noindent%
\begin{minipage}{0.19\textwidth}
\begin{minted}[linenos=false]{java}
if (e == f) c;
\end{minted}
\end{minipage} or
\begin{minipage}{0.355\textwidth}
\begin{minted}[linenos=false]{java}
if (e != f) /*...*/ else c;
\end{minted}
\end{minipage}.
Expressions \code{e} and \code{f} are interchangeable.

\begin{listing}
\begin{minted}{\qllexer}
predicate controlByEqualityTest(Expr e, Expr f, Expr c) { #\qlbox#
	exists (ConditionBlock cb, EqualityTest eqe |
		eqe.hasOperands(e, f) and eqe = cb.getCondition() and (
			(eqe.getOp() = " == " and cb.controls(c.getBasicBlock(), true)) or
			(eqe.getOp() = " != " and cb.controls(c.getBasicBlock(), false))
		)
	)
}
\end{minted}
\caption{\code{controlByEqualityTest} predicate definition.}
\label{lst:ql:controlByEqualityTest}
\end{listing}

Similar to the previous predicate,
this predicate holds with expressions \code{e}, \code{f}, and \code{c} such that 
\noindent%
\begin{minipage}{0.26\textwidth}
\begin{minted}[linenos=false]{java}
if (e.equals(f)) c;
\end{minted}
\end{minipage}.
Expressions \code{e} and \code{f} are interchangeable.

\begin{listing}
\begin{minted}{\qllexer}
predicate controlByEqualsMethod(Expr e, Expr f, Expr c) { #\qlbox#
	exists (ConditionBlock cb, MethodAccess ema |
		ema.getMethod() instanceof EqualsMethod and (
			(ema.getQualifier() = e and ema.getArgument(0) = f) or
			(ema.getQualifier() = f and ema.getArgument(0) = e)
		) and (
			ema = cb.getCondition() and cb.controls(c.getBasicBlock(), true)
		)
	)
}
\end{minted}
\caption{\code{controlByEqualsMethod} predicate definition.}
\label{lst:ql:controlByEqualsMethod}
\end{listing}

This predicate holds whenever \code{sub} is direct or indirect subclass of \code{sup}, or whenever both are the same type.

\begin{listing}
\begin{minted}{\qllexer}
predicate isSubtype(RefType sub, RefType sup) { #\qlbox#
	sub.getASupertype*() = sup
}
\end{minted}
\caption{\code{isSubtype} predicate definition.}
\label{lst:ql:isSubtype}
\end{listing}

The following class represents all casts guarded with a \code{getClass} comparison in an \code{equals} method.

\begin{listing}
\begin{minted}{\qllexer}
class GetClassGuardsVarCast extends #\qlref{Cast}# { #\qlbox#
	GetClassMethodAccess tma;
	GetClassMethodAccess oma;
	GetClassGuardsVarCast() {
		tma.isOwnMethodAccess() and
		oma.getQualifier() = this.(VarCast).getVar().getAnAccess() and
		(
			#\qlref{controlByEqualityTest}#(tma, oma, this) or
			#\qlref{controlByEqualsMethod}#(tma, oma, this)
		)
	}
}
\end{minted}
\caption{\code{GetClassGuardsVarCast} class definition.}
\label{lst:ql:GetClassGuardsVarCast}
\end{listing}

These classes define the AutoValue related classes used in the \nameref{pat:Equals} pattern.

\begin{listing}
\begin{minted}{\qllexer}
class AutoValueAnnotation extends Annotation { #\qlbox#
	AutoValueAnnotation() {
		getType().hasQualifiedName("com.google.auto.value", "AutoValue")
	}
}

class AutoValueClass extends Class {
	AutoValueClass() {
		getAnAnnotation() instanceof AutoValueAnnotation and
		isAbstract()
	}
}

class AutoValueGenerated extends Class {
	AutoValueGenerated() {
		count(getASupertype()) = 1 and
		getASupertype() instanceof AutoValueClass
	}
}
\end{minted}
\caption{\code{AutoValueGenerated} class definition.}
\label{lst:ql:AutoValueGenerated}
\end{listing}

This class represents a \code{newInstance} method,
used in the detection of the \nameref{pat:NewDynamicInstance} pattern.

\begin{listing}
\begin{minted}{\qllexer}
class NewDynamicInstanceAccess extends MethodAccess  { #\qlbox#
	NewDynamicInstanceAccess() {
		getCallee().hasName("newInstance") and (
			getCallee().getDeclaringType() instanceof TypeClass or
			getCallee().getDeclaringType() instanceof TypeConstructor or
			getCallee().getDeclaringType() instanceof TypeArray
		)
	}
}
\end{minted}
\caption{\code{NewDynamicInstanceAccess} class definition.}
\label{lst:ql:NewDynamicInstanceAccess}
\end{listing}


These classes represent either a \code{Method.invoke} or \code{Field.get} method access.
It is used in the detection of the \nameref{pat:ReflectiveAccessibility} pattern.
To implement this query, we made the \code{ReflectiveMethodAccess} class 
\emph{abstract}.

In \ql{}, abstract classes allow the developer 
``to think of a class as being the union of its subclasses''.%
\urlnote{https://help.semmle.com/QL/learn-ql/ql/advanced/abstract-classes.html}
The semantics of an abstract class is as follows:
``an abstract class has one or more superclasses and a characteristic predicate.
However, for a value to be in an abstract class,
it must not only satisfy the character of the class itself,
but it must also satisfy the character of a subclass.''
Thus,
for a value---here, a method access---to be in the \code{ReflectiveMethodAccess} class,
it needs to be either a method access in the \code{MethodInvokeMethodAccess} or \code{FieldGetMethodAccess} classes.

\begin{listing}
\begin{minted}{\qllexer}
abstract class ReflectiveMethodAccess extends MethodAccess {} #\qlbox#

class MethodInvokeMethodAccess extends ReflectiveMethodAccess {
	MethodInvokeMethodAccess() {
		getMethod().hasName("invoke") and
		getMethod().getDeclaringType()
						.hasQualifiedName("java.lang.reflect", "Method")
	}
}

class FieldGetMethodAccess extends ReflectiveMethodAccess {
	FieldGetMethodAccess() {
		getMethod().hasName("get") and
		getMethod().getDeclaringType()
						.hasQualifiedName("java.lang.reflect", "Field")
	}
}
\end{minted}
\caption{\code{ReflectiveMethodAccess} class definition.}
\label{lst:ql:ReflectiveMethodAccess}
\end{listing}

The following class represents an invocation to the \code{setAccessible(true)} method either on a \code{Field} or \code{Method} object.
It is used in the detection of the \nameref{pat:ReflectiveAccessibility} pattern.

\begin{listing}
\begin{minted}{\qllexer}
class SetAccessibleTrueMethodAccess extends MethodAccess { #\qlbox#
	Argument flagArgument;
	SetAccessibleTrueMethodAccess() {
		getMethod().hasName("setAccessible") and
		getMethod().getDeclaringType()
				.hasQualifiedName("java.lang.reflect", "AccessibleObject") and
		(
			(getNumArgument() = 1 and flagArgument = getArgument(0)) or
			(getNumArgument() = 2 and flagArgument = getArgument(1))
		) and
		flagArgument.(BooleanLiteral).getBooleanValue() = true
	}	
}
\end{minted}
\caption{\code{SetAccessibleTrueMethodAccess} class definition.}
\label{lst:ql:SetAccessibleTrueMethodAccess}
\end{listing}

This predicate holds whenever \code{type} is neither
a raw type (\eg{}, \code{List}),
a parameterized type (\eg{}, \code{List<String>}), nor
a bounded type (\ie{}, a type parameter or a wildcard).
It is used in the detection of the \nameref{pat:ImplicitIntersectionType}.

\begin{listing}
\begin{minted}{\qllexer}
predicate notGenericRelated(Type type) { #\qlbox#
	not type instanceof RawType and
	not type instanceof ParameterizedType and
	not type instanceof BoundedType
}
\end{minted}
\caption{\code{notGenericRelated} predicate definition.}
\label{lst:ql:notGenericRelated}
\end{listing}
